---
title: "Exploratory Data Analysis and Visualization"
subtitle: "Lecture 03"
author: "Vivek Srikrishnan"
course: "BEE 4850"
institution: "Cornell University"
date: "January 28, 2026"
format:
    revealjs: default
engine: julia
filters:
  - code-fullscreen
---

```{julia}
#| output: false

import Pkg
Pkg.activate(@__DIR__)
Pkg.instantiate()
```

```{julia}
#| output: false

using Random
using Distributions
using ColorSchemes
using CSV
using DataFrames
using DataFramesMeta
using Plots
using StatsPlots
using StatsBase
using LaTeXStrings
using Measures

Random.seed!(1)

plot_font = "Computer Modern"
default(
    fontfamily=plot_font,
    linewidth=3, 
    framestyle=:box, 
    label=nothing, 
    grid=false,
    guidefontsize=18,
    legendfontsize=16,
    tickfontsize=16,
    titlefontsize=20,
    bottom_margin=10mm,
    left_margin=5mm
)
```

# Review

## Probability Fundamentals

- Bayesian vs. Frequentist Interpretations
- Distributions reflect assumptions on probability of data.
- Normal distributions: "least informative" distribution for a given mean/variance.
- Fit distributions by maximizing likelihood.
- Communicating uncertainty: confidence vs. predictive intervals.


# Exploratory Data Analysis


## You Can Always Fit Models...

:::: {.columns}
::: {.column width=50%}
But not all models **are theoretically justifiable**.
:::
::: {.column width=50%}
![Ian Malcolm meme](memes/ian_malcolm_should_model.png)
:::
::::

## Some Implications of Mis-Specification

- Biased estimates (expected value from the model does not match "true" expected value);
- Incorrect inferences or inappropriate understandings of relationships/causality.
- Over/under-confident risk assessments.

## Data Generation Approximates Reality

:::: {.columns}
::: {.column width=33%}
![Estimand Estimator Cake](memes/estimand_cake.png){width=100%}
:::
::: {.column width=33%}
::: {.fragment .fade-in}
![Estimand Estimator Cake](memes/estimator_cake.png){width=100%}
:::
:::
::: {.column width=33%}
::: {.fragment .fade-in}
![Estimate Cake](memes/estimate_cake.png){width=100%}
:::
:::
::::

::: {.caption}
Source: Richard McElreath
:::

::: {.notes}
Goal is to start with some "true" process, then apply a procedure (experimental/observational + statistical) and recover what is hopefully a good estimate.

But lots can go wrong in this process!
:::


## How Do We Choose What To Model?

![XKCD 2620](https://imgs.xkcd.com/comics/health_data.png)

::: {.caption}
Source: [XKCD 2620](https://xkcd.com/2620/)
:::

## How Do We Choose What To Model?

Developing suitable models often starts with both **exploratory data analysis (EDA)** and **theoretical reasoning**.

::: {.incremental}
1. **EDA**: examining patterns/relationships with visual (plots, clustering) or quantitative (correlations).
2. **Theory**: Avoids "data dredging" or "mining," which can find spurious correlations or patterns which are misleading without broader context about data-generating mechanisms.
:::


## Exploratory Data Analysis

- Will see many examples of this throughout the semester.
- Try to impose as few assumptions as possible.
- Goal of exploratory analysis is to **get a high-level view of the data** and **formulate hypotheses/check if data is fit for purpose**.

## EDA Questions

EDA is about generating questions and identifying if the data quality is sufficient to address them.

Common questions to guide EDA:

1. **What type of variability occurs within my variables?**
2. **What type of covariation occurs between my variables?**

## Common EDA Questions

- What is the range of the data? What are "typical" values?
- What values are rare? Do they make sense?
- Are there missing data or strange outliers? What might explain them?
- Is a particular relationship or distribution supported by the data?


## Common EDA Approaches

- Data summaries (quantiles, mean/median, max/min, etc.)
- Correlations
- **Plot data** (from multiple perspectives)
- Clustering (do you need multiple models?)

## Example: NY Air Quality Dataset

```{julia}
#| echo: true
#| code-fold: true
#| output: true

aq = DataFrame(CSV.File("data/airquality/airquality.csv"))
rename!(aq, :"Solar.R" => :Solar) # rename solar radiation column to get rid of period
aq[1:5, :]
```

## Quantitative EDA: Summaries

```{julia}
#| echo: true
#| code-fold: true
#| output: true

aq_stack = stack(aq, 2:5)
aq_gp = @groupby(aq_stack, :variable)
@combine(aq_gp, $AsTable = (
        min=minimum(skipmissing(:value)), 
        median=median(skipmissing(:value)),
        mean=mean(skipmissing(:value)),
        max=maximum(skipmissing(:value)),
        missings=sum(ismissing.(:value))
    )
)

```

## Quantitative EDA: Correlations

```{julia}
#| echo: true
#| code-fold: true
#| output: true

aq_cor = cor(Matrix(dropmissing(aq[:, 2:5])))
DataFrame(aq_cor, names(aq[:, 2:5]))
```

# Visual EDA

## Anscombe's Quartet

:::: {.columns}

::: {.column width=40%}
Four datasets, all with the same means, variances, correlations, and regression lines.

Shows the importance of visualization!
:::
::: {.column width=60%}
![Anscombe's Quartet](figures/Anscombe.svg)

::: {.caption}
Source: [Wikipedia](https://en.wikipedia.org/wiki/Anscombe%27s_quartet)
:::
:::
::::


## Visualizations for EDA

- Often useful to start with **scatterplots**: one variable (typically independent) on the $x$-axis, another (typically dependent) on the $y$-axis.
- Time series: **time always goes on the $x$-axis**.
- Boxplots for comparison quantiles/ranges of variables or groups of data.
- Q-Q Plots to look for mismatches between distributions and data.

## Q-Q Plots

::: {.columns}
::: {.column width=50%}
One exploratory method to see if your data is reasonably described by a theoretical distribution is a **Q-Q plot**.
:::
::: {.column width=50%}
```{julia}
#| label: fig-norm-qq
#| code-fold: true
#| code-overflow: wrap
#| echo: true

samps = rand(Normal(0, 3), 20)
qqplot(Normal, samps, linewidth=3, markersize=6)
xlabel!("Theoretical Quantiles")
ylabel!("Empirical Quantiles")
plot!(size=(500, 450))
```
:::
::::


## Q-Q Plots: Data Overdispersed

```{julia}
#| label: fig-qq-1
#| echo: false
#| fig-cap: "Diagnosing Problems With Q-Q Plots"

# plot 1: data is more spread out
samps = rand(Normal(0, 3), 50)
p1 = qqplot(Normal(0, 1), samps, xlabel="Theoretical Quantiles", ylabel="Empirical Quantiles", linewidth=3, markersize=6)
p2 = plot(Normal(0, 1), linewidth=3, color=:blue, label="Reference Distribution")
scatter!(p2, samps, zeros(length(samps)), markersize=3, color=:black, label="Data")
plot(p1, p2, layout=(1, 2), size=(1200, 450))
```

## Q-Q Plots: Data Underdispersed

```{julia}
#| label: fig-qq-2
#| echo: false
#| fig-cap: "Diagnosing Problems With Q-Q Plots"

# plot 1: data is more spread out
samps = rand(Normal(0, 0.25), 50)
p1 = qqplot(Normal(0, 1), samps, xlabel="Theoretical Quantiles", ylabel="Empirical Quantiles", linewidth=3, markersize=6)
p2 = plot(Normal(0, 1), linewidth=3, color=:blue, label="Reference Distribution")
scatter!(p2, samps, zeros(length(samps)), markersize=3, color=:black, label="Data")
plot(p1, p2, layout=(1, 2), size=(1200, 450))
```

## Q-Q Plots: Data Skewed

```{julia}
#| label: fig-qq-3
#| echo: false
#| fig-cap: "Diagnosing Problems With Q-Q Plots"

# plot 1: data is more spread out
samps = rand(SkewNormal(0, 1, 0.5), 50)
p1 = qqplot(Normal(0, 1), samps, xlabel="Theoretical Quantiles", ylabel="Empirical Quantiles", linewidth=3, markersize=6)
p2 = plot(Normal(0, 1), linewidth=3, color=:blue, label="Reference Distribution")
scatter!(p2, samps, zeros(length(samps)), markersize=3, color=:black, label="Data")
plot(p1, p2, layout=(1, 2), size=(1200, 450))
```

## Q-Q Plots: Data Biased

```{julia}
#| label: fig-qq-4
#| echo: false
#| fig-cap: "Diagnosing Problems With Q-Q Plots"

# plot 1: data is more spread out
samps = rand(Normal(0.5, 1), 50)
p1 = qqplot(Normal(0, 1), samps, xlabel="Theoretical Quantiles", ylabel="Empirical Quantiles", linewidth=3, markersize=6)
p2 = plot(Normal(0, 1), linewidth=3, color=:blue, label="Reference Distribution")
scatter!(p2, samps, zeros(length(samps)), markersize=3, color=:black, label="Data")
plot(p1, p2, layout=(1, 2), size=(1200, 450))
```


## Q-Q Plots With Fitted Distributions

```{julia}
#| label: fig-cauchy-qq
#| echo: false
#| layout-nrow: 2
#| fig-cap: "Q-Q Plot for Cauchy Data and Normal Distribution"
#| fig-subcap: 
#|  - "Normal vs Cauchy Distribution"
#|  - "Q-Q Plot"

## generate fat-tailed residuals
cauchy_samps = rand(Cauchy(0, 0.05), 50)

# make plots
# scatterplot of observations
p1 = plot(fit(Normal, cauchy_samps), linewidth=3, color=:green, label="Normal Distribution", yaxis=false, legend=:outerright)
plot!(p1, fit(Cauchy, cauchy_samps), linewidth=3, color=:orange, linestyle=:dash, label="Cauchy Distribution")
scatter!(p1, cauchy_samps, zeros(length(cauchy_samps)), markersize=3, color=:black, label="Data")
xlims!(p1, (-2, 2))
xlabel!("Value")
plot!(p1, size=(1000, 250))

# densities of residual distributions
p2 = qqplot(Normal, cauchy_samps, linewidth=3, markersize=6, title="Normal Q-Q Plot")
xlabel!(p2, "Theoretical Values")
ylabel!(p2, "Empirical Values")
p3 = qqplot(Cauchy, cauchy_samps, linewidth=3, markersize=6, title="Cauchy Q-Q Plot")
xlabel!(p3, "Theoretical Values")
ylabel!(p3, "Empirical Values")
p = plot(p2, p3, layout=(1, 2), size=(900, 350))

display(p1)
display(p)
```

## Multi-Modal Data and Q-Q Plots

```{julia}
#| label: fig-mixture-qq
#| echo: false
#| layout-nrow: 2
#| fig-cap: "Q-Q Plot for Cauchy Data and Normal Distribution"
#| fig-subcap: 
#|  - "Normal vs Cauchy Distribution"
#|  - "Q-Q Plot"

## generate fat-tailed residuals
mm_samps = rand(MixtureModel(Normal,
    [(-1, 0.25), (1, 0.25)]), 
50)


# make plots
# scatterplot of observations
p1 = plot(fit(Normal, mm_samps), linewidth=3, color=:green, label="Normal Distribution", yaxis=false, legend=:outerright)
scatter!(p1, mm_samps, zeros(length(mm_samps)), markersize=3, color=:black, label="Data")
xlims!(p1, (-2, 2))
xlabel!("Value")
plot!(p1, size=(1000, 250))
plot!(p1, Normal(-1, 0.25), linewidth=3, color=:orange, linestyle=:dash, label="Mixture Distribution")
plot!(p1, Normal(1, 0.25), linewidth=3, color=:orange, linestyle=:dash, label=false)

# densities of residual distributions
p2 = qqplot(Normal, mm_samps, tickfontsize=16, guidefontsize=18, linewidth=3, markersize=6, title="Normal Q-Q Plot")
xlabel!(p2, "Theoretical Values")
ylabel!(p2, "Empirical Values")
p = plot!(p2, size=(900, 350))

display(p1)
display(p)
```


## Boxplots vs Histograms

:::: {.columns}
::: {.column width=50%}
**Boxplots**: Show quantiles (usually median + 1.5 $\times$ IQR)

**Histogram**: See distribution of data.
:::
::: {.column width=50%}
```{julia}
#| code-fold: true
#| echo: true
#| label: fig-explore1
#| fig-cap: Paired plots of air quality dataset

p1 = boxplot(skipmissing(aq[!, :Ozone]), ylabel="Ozone (ppb)")

p2 = histogram(skipmissing(aq[!, :Ozone]), ylabel="Ozone (ppb)")

p = plot(p1, p2, layout=(2, 1), size=(550, 500))

```
:::
::::

## Scatterplots

```{julia}
#| code-fold: true
#| echo: true
#| label: fig-explore2
#| fig-cap: Paired plots of air quality dataset

# this uses the dataframe plotting syntax from StatsPlots.jl
p1 = @df aq scatter(:Ozone, :Solar, markersize=5, color=:blue, xlabel="Ozone (ppb)", ylabel="Solar Radiation (lang)", leftmargin=10mm)
p2 = @df aq scatter(:Ozone, :Wind, markersize=5, color=:blue, xlabel="Ozone (ppb)", ylabel="Wind Speed (mph)", leftmargin=10mm)
p3 = @df aq scatter(:Ozone, :Temp, markersize=5, color=:blue, xlabel="Ozone (ppb)", ylabel="Max Temperature (Â°F) ", leftmargin=10mm)

p = plot(p1, p2, p3, layout=(1, 3), size=(1100, 400))

```


# Key Points


## EDA

- **EDA**: Understand structure of data while making as few assumptions as possible (e.g. look at the raw data, not "smoothed" versions).
- Need to be willing to ask many questions of your data: most won't make sense once you look at the data.
- You might be asking the "right" questions but have the "wrong" data: be open-minded and rely on domain knowledge.

## Visual EDA

- Need to experiment with different visualizations.
- Q-Q plots can help explore consistency with distribution assumptions.
- Think about whether you think the distribution of the data is meaningful.


# Upcoming Schedule

## Next Classes

**Friday**: Data Visualization

**Next Week**: Linear Regression and Fitting Models


## Assessments

**Homework 1** Due Friday, 2/6.

**Exercises**: This week's involves some simple computations.

**Reading**: Franconeri et al (2024): long review, recommend starting early!

# References

## References (Scroll for Full List)
